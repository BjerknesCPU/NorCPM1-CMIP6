
#======================================================================
# Clear coupler log files for comparison tests
#======================================================================
unsetenv CPLLOG_INIT
unsetenv CPLLOG_REST

#======================================================================
# do a 10 day mct test
#======================================================================
cd $CASEROOT
echo "doing a 10 day mct test" >>& $TESTSTATUS_LOG 

./xmlchange -file env_run.xml -id CONTINUE_RUN -val FALSE
./xmlchange -file env_run.xml -id STOP_OPTION  -val ndays 
./xmlchange -file env_run.xml -id STOP_N       -val 10 
./xmlchange -file env_run.xml -id HIST_OPTION  -val ndays 
./xmlchange -file env_run.xml -id HIST_N       -val 8
./xmlchange -file env_build.xml -id COMP_INTERFACE -val MCT
./xmlchange -file env_build.xml -id USE_ESMF_LIB -val TRUE

./$CASE.build
./$CASE.run

gunzip $RUNDIR/cpl.log*.gz
set CplLogFile = `ls -1t $RUNDIR/cpl.log* | head -1`
setenv CPLLOG_INIT $CplLogFile

#======================================================================
# do a 10 day esmf test
#======================================================================
cd $CASEROOT
echo "doing a 10 day esmf test" >>& $TESTSTATUS_LOG 

./xmlchange -file env_build.xml -id COMP_INTERFACE -val ESMF

./$CASE.clean_build
./configure -cleanall
./configure -case4test
./$CASE.build
./$CASE.run

./xmlchange -file env_build.xml -id COMP_INTERFACE -val MCT
./$CASE.clean_build
./configure -cleanall
./configure -case4test

gunzip $RUNDIR/cpl.log*.gz
set CplLogFile = `ls -1t $RUNDIR/cpl.log* | head -1`
setenv CPLLOG_REST $CplLogFile

#======================================================================
# runs complete
#======================================================================

./xmlchange -file env_run.xml -id CONTINUE_RUN -val FALSE
./xmlchange -file env_run.xml -id STOP_OPTION  -val ndays 
./xmlchange -file env_run.xml -id STOP_N       -val 10 
./xmlchange -file env_run.xml -id HIST_OPTION  -val ndays 
./xmlchange -file env_run.xml -id HIST_N       -val 8

setenv CPLLOG_GENCMP $CPLLOG_REST
echo "Initial Test log is $CPLLOG_INIT" >>& $TESTSTATUS_LOG 
echo "Restart Test log is $CPLLOG_REST" >>& $TESTSTATUS_LOG 

#======================================================================
# Check case validation first
#======================================================================

set basestatus = "UNDEF"

if ( $?CPLLOG_INIT ) then
  if ( $?CPLLOG_REST ) then
    echo "Comparing initial log file with second log file" >>& $TESTSTATUS_LOG
    $CASETOOLS/check_exactrestart.pl $CPLLOG_INIT $CPLLOG_REST >>& $TESTSTATUS_LOG
    set pass = `tail -1 $TESTSTATUS_LOG | grep PASS | wc -l`
    if ( $pass != 0 ) then
      set basestatus = "PASS "
    else
      set basestatus = "FAIL "
    endif
  else
    echo "ERROR in ${0}:  restart coupler log file NOT set" >>& $TESTSTATUS_LOG
    set basestatus = "ERROR"
  endif
else
  echo "ERROR in ${0}:  initial coupler log file NOT set" >>& $TESTSTATUS_LOG
  set basestatus = "ERROR"
endif

echo "$basestatus ${CASEBASEID} " >&! $TESTSTATUS_OUT

